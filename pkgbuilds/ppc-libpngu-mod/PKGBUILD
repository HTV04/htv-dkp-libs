# Maintainer: HTV04

_reponame=libpngu-mod

pkgname=ppc-${_reponame}
pkgver=r6.3e9b58e
pkgrel=1
pkgdesc="A C/C++ 2D/3D graphics library for Wii application developers"
arch=('any')
url="https://github.com/HTV04/${_reponame}"
license=("MIT")
depends=('ppc-libpng')
makedepends=('devkitPPC')
options=('!strip' 'libtool' 'staticlibs')
source=("git+${url}.git")
sha256sums=('SKIP')

pkgver() {
  cd "${_reponame}"

  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${_reponame}"

  make -C src
}

package() {
  cd "${_reponame}"

  # Install license
  install -Dm0644 LICENSE "${pkgdir}/opt/devkitpro/portlibs/ppc/licenses/ppc-${_reponame}/LICENSE"

  make -C src DESTDIR="${pkgdir}" install
}
