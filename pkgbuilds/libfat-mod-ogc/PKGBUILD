# Maintainer: HTV04

_reponame=libfat-mod

pkgname=${_reponame}-ogc
pkgver=1.1.3.r22.g219d553
pkgrel=1
pkgdesc="Nintendo Gamecube and Wii FAT library."
arch=('any')
url="http://github.com/HTV04/${_reponame}"
license=('custom')
depends=('libogc-mod')
groups=('wii-dev' 'gamecube-dev')
makedepends=("devkitPPC")
options=('!strip' 'libtool' 'staticlibs')
source=("git+${url}.git")
sha256sums=('SKIP')

pkgver() {
  cd "${_reponame}"

  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  make -C "${srcdir}/${_reponame}" ogc-release
}

package() {
  make -C "${srcdir}/${_reponame}" DESTDIR="${pkgdir}" ogc-install
}
