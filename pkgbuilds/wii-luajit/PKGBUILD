# Maintainer: HTV04

_reponame=LuaJIT

pkgname=wii-luajit
pkgver=2.1.0.beta3.r445.g09c8767e
pkgrel=1
pkgdesc="Nintendo Gamecube and Wii library."
arch=('any')
url="http://github.com/HTV04/${_reponame}"
license=('custom')
groups=('wii-portlibs')
makedepends=('devkitPPC' 'dkp-toolchain-vars')
options=('!strip' 'libtool' 'staticlibs')
source=("git+${url}.git")
sha256sums=('SKIP')

pkgver() {
  cd "${_reponame}"

  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "${_reponame}"

  ./build.sh wii
}

package() {
  cd "${_reponame}"

  ./install.sh wii "${pkgdir}"
}
