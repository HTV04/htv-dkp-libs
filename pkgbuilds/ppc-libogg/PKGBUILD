# Maintainer: HTV04
# Contributor:  Dave Murphy <davem@devkitpro.org>

pkgname=ppc-libogg
pkgver=1.3.5
pkgrel=1
pkgdesc='The Ogg transport bitstream is designed to provide framing, error protection and seeking structure for higher-level codec streams that consist of raw, unencapsulated data packets, such as the Opus, Vorbis and FLAC audio codecs or the Theora and Dirac video codecs.'
arch=('any')
url='https://wiki.xiph.org/Ogg'
license=(Xiph.org)
options=(!strip libtool staticlibs)
source=("https://downloads.xiph.org/releases/ogg/libogg-$pkgver.tar.xz")
sha256sums=('0eb4b4b9420a0f51db142ba3f9c64b333f826532dc0f48c6410ae51f4799b664')
makedepends=('ppc-pkg-config' 'dkp-toolchain-vars')
groups=('ppc-portlibs')
build() {
  cd libogg-$pkgver

  source /opt/devkitpro/devkitppc.sh
  source /opt/devkitpro/ppcvars.sh

  ./configure --prefix="${PORTLIBS_PREFIX}" --host=powerpc-eabi \
    --disable-shared --enable-static

  make
}

package() {
  cd libogg-$pkgver

  source /opt/devkitpro/devkitppc.sh
  source /opt/devkitpro/ppcvars.sh

  make DESTDIR="$pkgdir" install

  install -Dm644 COPYING "$pkgdir"${PORTLIBS_PREFIX}/licenses/$pkgname/COPYING

  # remove useless documentation
  rm -r "$pkgdir"${PORTLIBS_PREFIX}/share/doc
}
